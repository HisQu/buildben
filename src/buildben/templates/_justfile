# Template from buildben copied to <my_project>/justfile
# This is a collection of recipes callable from the CLI
# Just ask ChatGPT for recipes, or see https://just.systems/man/en/
# Dependencies: python3, direnv

# Allow `just deps-sync --upgrade --dry-run` pattern
set shell := ["bash", "-cu"]



# === Shortcuts / CI entrypoint =======================================

# Runs when you type just
default: 
    just --list


# === Virtual-environment =============================================

PY := "${VIRTUAL_ENV-}/bin/python"

# Hidden helper that must succeed first
[private]
_venv-guard:
    @test -n "${VIRTUAL_ENV-}" || { \
        echo "✗ Virtual environment inactive — run 'direnv reload' first" >&2; \
        exit 1; \
    }
    
# Deletes .direnv, recreates it, installs deps+dev and project (editable)
reset-venv:
    rm -rf .direnv        
    direnv reload   # direnv will rebuild on reload
    direnv exec . just install  # Make requirements.txt & install deps


# === Installation ====================================================

# Reinstall the project itself, editable mode
reinstall-proj:
    ### Make sure .direnv is active or venv
    just _venv-guard
    {{PY}} -m pip uninstall -y "${PROJECT_NAME}"
    {{PY}} -m pip install -e .[dev]
    

# Creates or refreshes the venv that `layout python` manages.
# Freeze/lock → install requirements.txt + dev-requirements.txt
install *ARGS:
    just _venv-guard
    pip-compile pyproject.toml -o requirements.txt {{ARGS}}
    pip-compile --extra dev pyproject.toml -o dev-requirements.txt {{ARGS}}
    pip-sync requirements.txt dev-requirements.txt  
    {{PY}} -m pip install -e .[dev]


# Upgrade all pins in-place
upgrade:                        
    just install --upgrade



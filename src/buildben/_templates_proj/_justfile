# Template from buildben copied to <my_project>/justfile
# This is a collection of recipes callable from the CLI
# Helpful links for just:
# - Documentation: https://just.systems/man/en/
# - Cheat Sheet: https://cheatography.com/linux-china/cheat-sheets/justfile/
# Dependencies: python3, direnv

# Allow `just deps-sync --upgrade --dry-run` pattern
set shell := ["bash", "-cu"]



# === Shortcuts / CI entrypoint =======================================

# Runs when you type just
default: 
    just --list


# =====================================================================
# === Virtual-environment 
# =====================================================================

### Store python executable from virtual environment
PY := "${VIRTUAL_ENV-}/bin/python"

# Hidden helper that must succeed first
[private]
_venv-guard:
    @test -n "${VIRTUAL_ENV-}" || { \
        echo "âœ— VIRTUAL_ENV is not set, Virtual environment inactive â€” run 'direnv reload' first" >&2; \
        exit 1; \
    }
    
# Deletes .direnv, recreates it, installs all requirements and <my_project> (editable)
reset-venv:
    rm -rf .direnv        
    direnv reload   # direnv will rebuild on reload
    direnv exec . just install-compile  # Make requirements.txt & install deps
alias resve := reset-venv

# =====================================================================
# === Installations
# =====================================================================

# Resolves dependency versions and creates requirements.txt + dev-requirements.txt
compile-requirements *ARGS:
    pip-compile pyproject.toml -o requirements.txt {{ARGS}}
    pip-compile --extra dev pyproject.toml -o dev-requirements.txt {{ARGS}}
alias comre:= compile-requirements

# Compiles all requirements & installs <my_project> in editable mode
install-compile *ARGS:
    just _venv-guard
    just compile-requirements {{ARGS}}
    just install
alias insco := install-compile

# Only Installs: requirements.txt & <my_project> in editable mode
install:
    just _venv-guard
    if ! [[ -e requirements.txt ]]; then echo "âœ— Didn't find requirements.txt! Run 'just install-compile'!" >&2; exit 1; fi
    pip-sync requirements.txt dev-requirements.txt  
    {{PY}} -m pip install -e .[dev]
alias ins := install

# Uninstalls <my_project>, and installs it in editable mode
reinstall:
    just _venv-guard
    {{PY}} -m pip uninstall -y "${PROJECT_NAME}"
    {{PY}} -m pip install -e .[dev]
alias reins := reinstall


# =====================================================================
# === Upgrade
# =====================================================================

# Upgrade all pins in-place
upgrade:                        
    just install-compile --upgrade


# Echo the full git+ URL for a dependency declared in pyproject.toml
giturl-from-toml dep:
    # Find first uncommented line with the dependency name and extract the git URL
    @git_url=$(grep -E "^[[:space:]]*\"{{dep}}[[:space:]]*@[[:space:]]*git\\+" pyproject.toml \
        | head -n1 \
        | sed -E 's/^.*(git\+[^@",#]+).*/\1/') ;\
    if [ -z "$git_url" ]; then \
        echo "ðŸ›‘  '{{dep}}' not found in pyproject.toml" >&2 ;\
        exit 1 ;\
    fi ;\
    echo "$git_url"
alias gurl := giturl-from-toml   # optional shorthand

# Install the dependency from its Git-URL without compiling requirements
install-giturl-from-toml dep:
    just _venv-guard
    url=$(just gurl {{dep}}) ;\
    {{PY}} -m pip install "$url"
    just compile-requirements 
alias insgurl := install-giturl-from-toml 



# %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
# === Add custom recipes below!
# %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%